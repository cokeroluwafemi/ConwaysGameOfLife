// Conway's Game of Life
// Steven Klise <http://stevenklise.com>
// 2011

// Based on: Conway's Game of Life, by Mike Davis. 

// Grid resizes based on window (jQuery)

// TODO:
// - Control Panel
// - Redefine forms to include dimensions in the first two spots
// - - "Glider","3,3,0,0,1,1,0,1,0,1,1"
// - - Will require redoing the draw function.

Grid board;
//boolean running;
int[][][] world;
boolean kill = false;

HashMap forms = new HashMap(20);

void setup()
{
	size(window.sketchWidth, window.sketchHeight);
	frameRate(window.speed);
	board = new Grid(3, 45, width-3, height-3, 10);
	window.running = false;

	world = new int[board.gw][board.gh][2];
	
	forms.put("BLOCK",					"2,2,1,1,1,1");
	forms.put("GLIDER",					"3,3,0,0,1,1,0,1,0,1,1");
	forms.put("BLINKER",				"3,3,0,1,0,0,1,0,0,1,0");
	forms.put("R-PENTOMINO",			"3,3,0,1,1,1,1,0,0,1,0");
	forms.put("BOAT",					"3,3,1,1,0,1,0,1,0,1,0");
	forms.put("HERSCHEL",				"3,4,1,0,0,1,1,1,1,0,1,0,0,1");
	forms.put("TOAD",					"4,2,0,1,1,1,1,1,1,0");
	forms.put("AIRCRAFT CARRIER",		"4,3,1,1,0,0,1,0,0,1,0,0,1,1");
	forms.put("BEEHIVE",				"4,3,0,1,1,0,1,0,0,1,0,1,1,0");
	forms.put("BEACON",					"4,4,1,1,0,0,1,0,0,0,0,0,0,1,0,0,1,1");
	forms.put("LOAF",					"4,4,0,1,1,0,1,0,0,1,0,1,0,1,0,0,1,0");
	forms.put("LIGHTWEIGHT SPACESHIP",	"5,4,0,1,0,0,1,1,0,0,0,0,1,0,0,0,1,1,1,1,1,0");
	forms.put("PULSAR","15,15,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,0,0,0,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,0,0,1,1,1,0,0,0,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,0,0,0,1,1,1,0,0,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,1,0,0,0,0,1,0,1,0,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,0,0,0,1,1,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0");
	forms.put("LIDKA","9,15,0,1,0,0,0,0,0,0,0,1,0,1,0,0,0,0,0,0,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0,0,0,0,0,1,0,1,0,0,0,0,0,1,1,0,1,0,0,0,0,0,0,0,0,0,0,0,0,0,1,1,1,0,0")
}

void draw()
{
	background(0);
	board.render();
	frameRate(window.speed);
	for (int x=0; x<board.gw; x++) // draw and update.
	{
		for (int y=0; y<board.gh; y++)
		{
			if (world[x][y][0] == 1)
			{
				PVector l = board.toScreen(x, y);
				rect(l.x, l.y, board.res, board.res);
			}
			int n = neighbors(x, y);
			if ((n < 2 || n > 3) && world[x][y][0] == 1)
			{
				world[x][y][1] = -1;
			}
			else if (n == 3 && world[x][y][0] == 0)
			{
				world[x][y][1] = 1;
			}
			else
			{
				world[x][y][1] = 0;
			}
		}
	}
	if (window.running)
	{
		for (int x=0; x<board.gw; x++)
		{
			for (int y=0; y<board.gh; y++)
			{
				if (world[x][y][1] == 1 || (world[x][y][1] == 0 && world[x][y][0] == 1))
				{
					world[x][y][0] = 1;
				}
				else if (world[x][y][1] == -1)
				{
					world[x][y][0] = 0;
				}
			}
		}
	}
	ifClear();
	//println("running");
}

void ifClear() // Function to clear the entire world.
{
	if(window.cclear)
	{
		for (int x=0; x<board.gw; x++)
		{
			for (int y=0; y<board.gh; y++)
			{
				world[x][y][0] = 0;
				world[x][y][1] = 0;
			}
		}
		window.cclear = false;
	}
}

void mouseDragged() // dragging the mouse is different than clicking. Requires mousePressed() for initial condition.
{
	if (mouseX > board.tx && mouseX < board.bx && mouseY < board.by && mouseY > board.ty && (window.cforms == undefined || window.cforms == ""))
	{
		PVector t = board.toGrid(mouseX, mouseY);
		if (!kill)
		{
			world[(int)t.x][(int)t.y][0] = 1;
		}
		else
		{
			world[(int)t.x][(int)t.y][0] = 0;
		}
	}
}

void mousePressed() // Did the user press on a live or dead cell?
{
	if (mouseX > board.tx && mouseX < board.bx && mouseY < board.by && mouseY > board.ty)
	{
		PVector l = board.toGrid(mouseX, mouseY);
		if (world[(int)l.x][(int)l.y][0] != 1)
		{
			kill = false;
		}
		else
		{
			kill = true;
		}
	}
}

void mouseClicked() // Single cell birth or death, as well as placing forms.
{
	if (mouseX > board.tx && mouseX < board.bx && mouseY < board.by && mouseY > board.ty)
	{
		PVector l = board.toGrid(mouseX, mouseY);
		if (mouseButton == LEFT)
		{
			if(window.cforms != undefined)
			{
				placeForm(window.cforms, (int)l.x,(int)l.y);
			}
			else
			{
				world[(int)l.x][(int)l.y][0] = (world[(int)l.x][(int)l.y][0]+1)%2;
			}
		}
	}
}

int neighbors(int x, int y)
{
	if(window.edges)
	{
		int n =0;
		if (x+1 < board.gw)
		{
			n += world[(x + 1)][y][0];
			if (y+1 < board.gh)
			{
				n += world[(x + 1)][(y + 1)][0];
			}
			if (y-1 >= 0)
			{
				n += world[(x + 1)][(y - 1)][0];
			}
		}
		if (x-1 >= 0)
		{
			n += world[(x - 1) % board.gw][y][0];
			if (y+1 < board.gh)
			{
				n += world[(x - 1)][(y + 1)][0];
			}
			if (y-1 >= 0)
			{
				n += world[(x - 1)][(y - 1)][0];
			}
		}
		if (y-1 >= 0)
		{
			n += world[x][(y - 1)][0];
		}

		if ( y+1 < board.gh )
		{
			n += world[x][(y + 1)][0];
		}
		return n;
	}
	else
	{
		return world[(x + 1) % board.gw][y][0] + 
			world[x][(y + 1) % board.gh][0] + 
			world[(x + board.gw - 1) % board.gw][y][0] + 
			world[x][(y + board.gh - 1) % board.gh][0] + 
			world[(x + 1) % board.gw][(y + 1) % board.gh][0] + 
			world[(x + board.gw - 1) % board.gw][(y + 1) % board.gh][0] + 
			world[(x + board.gw - 1) % board.gw][(y + board.gh - 1) % board.gh][0] + 
			world[(x + 1) % board.gw][(y + board.gh - 1) % board.gh][0];
	}
}


class Grid
{
	int tx;
	int ty;
	int bx;
	int by;
	int gw;
	int gh;
	int res;

	Grid(int _tx, int _ty, int _bx, int _by, int _res)
	{
		tx = _tx; 
		ty = _ty; 
		bx = _bx; 
		by = _by; 
		res = _res;
		gw = int((bx-tx)/res);
		gh = int((by-ty)/res);
	}

	void render()
	{
		stroke(60,60,60,255);
		for (int x=tx; x<=bx; x += res)
		{
			line(x,ty,x,gh*res+ty);
		}
		for (int y=ty; y<=by; y += res)
		{
			line(tx,y,gw*res+tx,y);
		}
	}

	PVector toGrid(int x, int y) // Convert a pixel number to a grid number
	{
		int gridx = (x - tx)/res;
		int gridy = (y - ty)/res;
		PVector v = new PVector(gridx,gridy);
		return v;
	}

	PVector toScreen(int x, int y) // Convert a grid number to a pixel number
	{
		int sx = x*res+tx;
		int sy = y*res+ty;
		PVector v = new PVector(sx,sy);
		return v;
	}
}

void placeForm(String thisform, int mx, int my)
{
	String form = (String)forms.get(thisform);
	String[] df = split(form,',');
	int[] intvals = int(split(form,','));
	int dx = intvals[0];
	int dy = intvals[1];
	for(int x = 0; x<dx; x++)
	{
		for(int y = 0; y<dy; y++)
		{
			world[mx+x][my+y][0] = intvals[2+x+dx*y];
		}
	}
}